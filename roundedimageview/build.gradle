apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

def sonatypeUsername = project.hasProperty('sonatypeUsername') ? sonatypeUsername : ""
def sonatypePassword = project.hasProperty('sonatypePassword') ? sonatypePassword : ""

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion 14
    consumerProguardFiles 'release-proguard.cfg'
    versionCode 1
    versionName version
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}

dependencies {
  compileOnly 'com.squareup.picasso:picasso:2.5.2'
  compileOnly 'androidx.annotation:annotation:1.1.0'
  compileOnly 'androidx.appcompat:appcompat:1.1.0'
}

task androidJavadocs(type: Javadoc) {
  source = android.sourceSets.main.java.source
}

task androidJavadocsJar(type: Jar) {
  classifier = 'javadoc'
  //basename = artifact_id
  from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  //basename = artifact_id
  from android.sourceSets.main.java.source
}

artifacts {
  archives androidSourcesJar
  archives androidJavadocsJar
}

signing {
  required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

